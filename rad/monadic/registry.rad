;; Helpers for setting up and interacting with the registry machine (as defined in
;; rad/monadic/registry-remote.rad)
{:module  'monadic/registry
 :doc     "Functions for interacting with registry machine"
 :exports '[create-registry-machine!]}

(import prelude/lens :unqualified)
(import prelude/machine '[send-code! send-signed-command!] :unqualified)
(import prelude/time '[install-fake-clock] :unqualified)
(import prelude/io :as 'io)
(import prelude/key-management '[use-fake-keys! get-keys!] :unqualified)

(def radicle-registry-machine-id
  "The name of this machine."
  "monadic/radicle/registry")

(def create-registry-machine!
  "Create a remote registry machine with the given url."
  (fn []
    (def owner-keys (lookup :public-key (get-keys!)))
    (def id (machine/new-machine!))
    (machine/send-prelude! id)
    (send-code! id (find-module-file! "monadic/registry-remote.rad"))
    (machine/send-signed-command! id radicle-registry-machine-id 'add-admin {:key owner-keys})
    id))

(def simple-registered-machine
  "Register a Radicle machine with no labels."
  (fn [i]
    (def t (now!))
    (<> {:labels      []
         :created-at  t
         :modified-at t}
        i)))

(def register-machine!
  "Register a new remote machine with the keys in `my-keys.rad`."
  (fn [machine i]
    (machine/send-signed-command! machine radicle-registry-machine-id 'register-machine i)))

(def edit-registered-machine!
  "Edit a remote registered machine with the keys in `my-keys.rad`."
  (fn [machine i]
    (machine/send-signed-command! machine radicle-registry-machine-id 'edit-registered-machine i)))

(def delete-registered-machine!
  "Delete a remote registered machine with the keys in `my-keys.rad`."
  (fn [machine i]
    (machine/send-signed-command! machine radicle-registry-machine-id 'delete-registered-machine i)))

(def simple-register-machine!
  "Register a remote machine with sensible defaults."
  (fn [machine name desc id]
    (register-machine! machine (simple-registered-machine {:name name :description desc :registered-machine-id id}))))

(def simple-edit-registered-machine!
  (fn [machine n i]
    (edit-registered-machine! machine
                 (<> {:registered-machine-number n
                      :modified-at (now!)}
                     i))))

(def list-registered-machines
  "Return the full map of registered machines."
  (fn [machine]
    (machine/query! machine '(list-registered-machines))))

(def short
  "Short render of a registered machine."
  (fn [i]
    (string-append
     (show (lookup :number i))
     ": "
     (lookup :name i))))

(def list-last
  "Get the last `n` regsistered machines."
  (fn [machine n]
    (def is (sort-by (fn [i] (- 0 (lookup :number i))) (values (list-registered-machines machine))))
    (take n is)))

(test
 "The monadic registry machine works."
 [:setup
  (do (machine/install-remote-machine-fake)
      (def owner-keys (gen-key-pair! (default-ecc-curve)))
      (def non-owner-keys (gen-key-pair! (default-ecc-curve)))
      (set-fake-keys! owner-keys)
      (def machine (create-registry-machine!))
      (simple-register-machine! machine "name0" "desc0" "123")
      (simple-register-machine! machine "name1" "desc1" "456")
      (set-fake-keys! non-owner-keys)
      (simple-register-machine! machine "will be deleted" "none" "111")
      (delete-registered-machine! machine {:registered-machine-number 2})
      (simple-register-machine! machine "new" "new" "111")
      (def is (list-registered-machines machine))
      (set-fake-keys! owner-keys))
 ]
 [ (length (seq is)) ==> 3 ]
 [ (view (.. (@ 0) (@ :name)) is) ==> "name0" ]
 [ (view (.. (@ 1) (@ :name)) is) ==> "name1" ]
 [ (short (first (values is))) ==> "0: name0" ]
 [ (short (first (list-last machine 1))) ==> "3: new" ]
 ;; authorization
 ;; owner can change the registered machine name
 [ (do
      (set-fake-keys! owner-keys)
      (catch 'validation-failure
             (do (simple-edit-registered-machine! machine 3 {:name "New name"})
                 :valid)
             (fn [_] :not-valid))) ==> :valid
 ]
 ;; author can change the registered machine name
 [ (do
      (set-fake-keys! non-owner-keys)
      (catch 'validation-failure
             (do (simple-edit-registered-machine! machine 3 {:name "New name"})
                 :valid)
             (fn [_] :not-valid))) ==> :valid
 ]
 ;; non-owner/non-author cannot change the registered machine name
 [ (do
      (set-fake-keys! non-owner-keys)
      (catch 'validation-failure
             (do (simple-edit-registered-machine! machine 0 {:name "New name"})
                 :valid)
             (fn [_] :not-valid))) ==> :not-valid
 ]
)
