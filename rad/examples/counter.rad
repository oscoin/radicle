;; A "counter" machine that exposes an `increment` mutator and a `get-value`
;; accessor.
;;
;; This file provides functions to interact with a counter machine.
;;
;;     (counter/init-machine! "my-machine-id") ;; => :ok
;;     (counter/get-value "my-machine-id") ;; => 0
;;     (counter/increment! "my-machine-id") ;; => 1
;;     (counter/get-value "my-machine-id") ;; => 1
;;

(load! "rad/prelude.rad")

(import prelude/test '[assert-equal] :unqualified)

(def counter/prelude
  '[
    (def counter (ref 0))
    (def increment
      "Increments the counter by one and returns the new counter."
      (fn []
        (def x (read-ref counter))
        (write-ref counter (+ x 1))))
    (def get-value
      "Return the current value of the counter"
      (fn []
        (read-ref counter)))
  ]
)

(def counter/init-machine!
  "Send the counter application prelude to a machine."
  (fn [machine-id]
    (machine/send! machine-id counter/prelude)
    :done
))

(def counter/increment!
  "Increment the counter of a machine and return the new counter value."
  (fn [machine-id]
    (def machine (machine/load-machine! machine-id))
    (def expr '(increment))
    (def result (lookup :result (machine/eval-in-machine expr machine)))
    (machine/send! machine-id [expr])
    result
))

(def counter/get-value
  "Get the current counter value of a machine."
  (fn [machine-id]
    (def machine (machine/load-machine! machine-id))
    (lookup :result (machine/eval-in-machine '(get-value) machine))
))

(def counter/run-test
  (fn []
    (def machine-id (string-append "http://radicle-server:8000/machines/" (uuid!)))
    (assert-equal (counter/init-machine! machine-id) :done)
    (assert-equal (counter/get-value machine-id) 0)
    (assert-equal (counter/increment! machine-id) 1)
    (assert-equal (counter/increment! machine-id) 2)
    (assert-equal (counter/get-value machine-id) 2)
    :test-ok
) )
