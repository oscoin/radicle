#!/usr/bin/env radicle
;;
;; Test correctness of the storage interface provided by the server.
;;
;; This script tests that the HTTP server implements the storage protocol with
;; `send!` and `receive!` properly.
;;
;; The script accepts the hostname of the server as an optional argument. The
;; hostname defaults to `localhost`. We always use port 8000.
;;


(load! "rad/prelude.rad")

(import prelude/patterns '[/cons] :unqualified)
(import prelude/test '[assert-equal] :unqualified)

(def host
  (match (get-args!)
    (/cons 'h _) h
    /nil         "localhost"
  ))

(def create-machine
  (fn []
    (string-append "http://" host ":8000/chains/" (uuid!))
  ))

(def exprs-1 [ :e1 :e2 ])
(def exprs-2 [ :f1 :f2 :f3 ])

(def test/receive-all
  "Passing `:nothing` to `receive!` returns all expressions"
  (fn []
    (def machine-id (create-machine))
    (send! machine-id exprs-1)
    (send! machine-id exprs-2)
    (def all-exprs (<> exprs-1 exprs-2))
    (match (receive! machine-id :nothing)
      [_ 'received-exprs] (assert-equal received-exprs all-exprs))
  ))

(def test/receive-last-index
  (fn []
    "Passing the last index from `send!` to `receive!` returns no expressions"
    (def machine-id (create-machine))
    (def index (send! machine-id exprs-1))
    (match (receive! machine-id [:just index])
      [_ 'received-exprs] (assert-equal received-exprs []))
  ))

(def test/receive-with-index
  (fn []
    "Passing an index from `send!` to `receive!` returns no expressions"
    (def machine-id (create-machine))
    (def index (send! machine-id exprs-1))
    (send! machine-id exprs-2)
    (match (receive! machine-id [:just index])
      [_ 'received-exprs] (assert-equal received-exprs exprs-2))
  ))

(def test/receive-returns-sent-index
  "`receive!` returns the last sent index"
  (fn []
    (def machine-id (create-machine))
    (def index-send (send! machine-id exprs-1))
    (match (receive! machine-id :nothing)
      ['index-receive _] (assert-equal index-receive index-send))
  ))

(test/receive-all)
(test/receive-last-index)
(test/receive-with-index)
(test/receive-returns-sent-index)
