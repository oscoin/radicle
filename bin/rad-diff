#!/usr/bin/env radicle

(load! (find-module-file! "prelude.rad"))
(load! (find-module-file! "monadic/diff.rad"))
(load! (find-module-file! "prelude/io-utils.rad"))
(load! (find-module-file! "monadic/project.rad"))

(import prelude/validation :as 'validation)

(def machine-name (fn [] (get-rsm-of-type! :rad-diff)))

(def valid-states
  ["pending" "retracted" "accepted" "rejected"])

(def status-opts ["-s" "--state" "--filter-by-state" ])

(def list-opts-help
  (string-append
    "[("
    (string/intercalate " | " status-opts)
    ") <state>] [--fancy]"))

(def help
  (string-append
  "rad-diff - Radicle Diff CLI

   Usage:
        rad-diff list " list-opts-help "
        rad-diff propose <commit>
        rad-diff [retract | accept | reject | show] <diff-no>
        rad-diff comment <diff-no> <comment>
        rad-diff help

     list         - Select from diffs to view or comment on
     propose      - Create a new diff in $EDITOR
     show         - Show a diff
     comment      - Shortcut for adding a comment
     retract      - Retract your own diff
     accept       - Accept a diff
     reject       - Reject a diff
     help         - Print this help and exit
  "))

(def cmd-parse-failure
  (fn [error]
    (parse-failure error help)))

(def verify-diff-number
  (fn [diff-no machine]
    (match (lookup-default diff-no #f (list-diffs machine))
      #f         (do
                   (put-str! (string-append "DIFF " (show diff-no) " does not exist."))
                   (exit! 1))
      'the-diff  the-diff)))

;; git interactions

(def verify-commit
  (fn [commit]
    (match (process-with-stdout-stderr-exitcode! "git" ["show" commit] "")
      [_ _ :ok] commit
      _         (do
                  (put-str! "Commit can't be found")
                  (exit! 1)))))

(def get-patch!
  (fn [commit]
    (string/unlines (process-with-stdout! "git" ["format-patch" "-1" commit "--stdout"] ""))))

(def get-title!
  (fn [commit]
    (string/unlines (process-with-stdout! "git" ["show" "-s" "--format=%s" commit] ""))))

(def get-description!
  (fn [commit]
    (string/unlines (process-with-stdout! "git" ["show" "-s" "--format=%b" commit] ""))))

(def get-author-name!
  (fn [commit]
    (string/unlines (process-with-stdout! "git" ["show" "-s" "--format=%aN" commit] ""))))

(def get-author-email!
  (fn [commit]
    (string/unlines (process-with-stdout! "git" ["show" "-s" "--format=%aE" commit] ""))))

(def get-short-hash!
  (fn [commit]
    (string/unlines (process-with-stdout! "git" ["rev-parse" "--short" commit] ""))))

(def get-git-username!
  (string/unlines (process-with-stdout! "git" ["config" "user.name"] "")))

(def apply-patch!
  "Given a patch, apply it to the current branch"
  (fn [patch]
    (def file (unlines (shell-with-stdout! "mktemp" "")))
    (put-str! file)
    (write-file! file patch)
    (process! "git" ["am" file] "")))

(def checkout-diff!
  "Creates a new branch diff/<DIFF-ID> based on the DIFF, and switches to it."
  (fn [machine diff-no]
    (def diff (verify-diff-number diff-no machine))
    (match (process! "git" ["checkout" "-b" (string-append "diff/" (show diff-no))] "")
      :ok (apply-patch! (lookup :patch diff))
      _   (put-str! (string-append "Command 'git checkout -b diff/" (show diff-no) "' failed")))))

(def push-diff!
  "Merges the DIFF to master, and pushes the changes."
  (fn [machine diff-no]
    (def diff (verify-diff-number diff-no machine))
    (process! "git" ["checkout" "master"] "")
    (match (apply-patch! (lookup :patch diff))
      :ok (do
            (process! "git" ["push"] ""))
      _   (do
            (put-str! "Resolve any conflicts and then run 'git push'")
            (exit! 1)))))

(def new-diff!
  "Creates a new diff on `machine` with a patch from the given `commit`."
  (fn [machine commit]
    (verify-commit commit)
    (def patch (get-patch! commit))
    (def author-name (get-author-name! commit))
    (def author-email (get-author-name! commit))
    (def short-hash (get-short-hash! commit))
    (def title (get-title! commit))
    (def description (get-description! commit))
    (def diff {:short-hash    short-hash
               :title         title
               :description   description
               :author-name   author-name
               :author-email  author-email
               :patch         patch})
    (simple-create-diff! machine diff)
    (put-str! (string-append "Proposing " short-hash " to " machine))))

;; showing diffs

(def pretty-state
  (fn [state]
    (drop 1 (show state))))

(def pretty-headline
  (fn [i]
    (match i
      '{:number n
        :title tit
        :state s
        :author-name an
        :short-hash sh}
      (string-append
        "(" (pretty-state s) ") [" an "] " sh " - " tit " | DIFF " (show n)))))

(def pretty-comment
 (fn [c]
   (match c
     '{:created-at c-a
       :body b
       :author-name a-n}
     (string-append "### " a-n " [" c-a "]\n\n" b "\n"))))

(def pretty-diff
  (fn [i]
    (match i
      '{:patch p
        :created-at c-a
        :comments cs}
      (string-append
        (pretty-headline i) "\n\n"
        "created at " c-a "\n\n"
        p "\n\n"
        "Comments\n"
        "--------\n\n"
        (string/unlines (map pretty-comment cs))))))

(def comment-divider "----------------------------------------")

(def add-comment-md
  (fn [x]
    (string-append
     "\n" comment-divider "\n"
     "---Add comment above the line.\n\n"
     x)))

(def list-items
  "Lists all diffs and filters them by `options`. If `--new` is passed as an
  option only recent items are shown. If `--fancy is passed, the list is opened
  via fzf."
  (fn [machine options]

    (def mk-key
      (fn [diff] [(pretty-headline diff) diff]))

    (def filt
      (match (map (fn [opt] (read (string-append ":" opt))) (lookup :state options))
        []      (fn [x] #t)
        'args   (fn [x] (elem? (lookup :state x) args))))

  (def items
    (map mk-key
      (filter filt
        (reverse (values (list-diffs machine))))))

    (def items-dict (dict-from-seq items))
    (if (empty-seq? items)
      (put-str! "No diffs!")
      (if (lookup :fancy options)
        (match (fzf-select-with-preview!
                 (map first items)
                 (fn [s] (pretty-diff (lookup s items-dict))))
               (/just 's)
               (do (def selected (lookup s items-dict))
                   (def diff-number (match selected {:number 'n} n))
                   (def pretty-item
                     (add-comment-md
                       (pretty-diff selected)))
                   (def edited-item (edit-in-editor! pretty-item))
                   (def added-comment
                     (string/unlines
                      (take-while (fn [x] (not (eq? x comment-divider)))
                                  (string/lines edited-item))))
                   (put-str!
                    (if (eq? added-comment "")
                      "Item was not updated."
                      (do (simple-add-comment! machine diff-number added-comment get-git-username!)
                          (string-append "Sent comment: " added-comment)))))
               _ (put-str! "No selection made."))
         (map (fn [d] (put-str! (first d))) items)))))

(def show-diff!
  "Shows a single DIFF `n`"
  (fn [machine n]
    (if (number? n)
      (if (integral? n)
        (do
          (def diff (verify-diff-number n machine))
          (put-str! (pretty-diff diff)))
        (cmd-parse-failure "Diff numbers must be whole numbers!"))
      (cmd-parse-failure "To show a diff, please provide the diff number."))))

(def edit-diff-state!
  "Edit the state of a DIFF. If the new state is `:accepted` the DIFF is then
  also merged to master and the changes pushed."
  (fn [machine n new-state]
    (if (number? n)
      (if (integral? n)
        (do
          (def edit-state-with-feedback!
            (fn [message]
              (simple-edit-diff! machine n {:state new-state})
              (put-str! message)))
          (verify-diff-number n machine)
          (if (eq? new-state :accepted)
            (match (push-diff! machine n)
              :ok (edit-state-with-feedback!
                    (string-append "Diff " (show n) " has been " (pretty-state new-state) " and merged."))
              _   (exit! 1))
            (edit-state-with-feedback!
              (string-append "Diff " (show n) " has been " (pretty-state new-state)))))
        (cmd-parse-failure "Diff numbers must be whole numbers!"))
    (cmd-parse-failure "To change the state, please provide the diff number."))))

(def create-comment!
  "Add a new `comment` to DIFF."
  (fn [machine n comment]
    (if (number? n)
      (if (integral? n)
        (do
          (verify-diff-number n machine)
          (simple-add-comment! machine n comment get-git-username!)
          (put-str! (string-append "Added comment to Diff " (show n))))
        (cmd-parse-failure "Diff numbers must be whole numbers!"))
    (cmd-parse-failure "To create a comment, please provide the diff number."))))

(def cmd-options
  [
    { :key :state :type :multi-opt :options status-opts :possible-values valid-states :default []}
    { :key :fancy :type :flag :options ["--fancy"] :default #f }
  ])

(def /list-cmd
  (fn [opts]
    (/cmd-opts "list" opts cmd-options help)))

(def args (get-args!))

(machine/catch-daemon!
 (fn []
   (match args
          (/list-cmd 'options) (list-items (machine-name) options)
          (/cmd-1 "propose" 'commit help) (new-diff! (machine-name) commit)
          (/cmd-1 "checkout" 'diff-no help) (checkout-diff! (machine-name) (read diff-no))
          (/cmd-1 "show" 'diff-no help) (show-diff! (machine-name) (read diff-no))
          (/cmd-1 "retract" 'diff-no help) (edit-diff-state! (machine-name) (read diff-no) :retracted)
          (/cmd-1 "accept" 'diff-no help) (edit-diff-state! (machine-name) (read diff-no) :accepted)
          (/cmd-1 "reject" 'diff-no help) (edit-diff-state! (machine-name) (read diff-no) :rejected)
          ["comment" 'diff-no 'comment] (create-comment! (machine-name) (read diff-no) comment)
          (/cmd-0 "create-machine" help) (new-diff-machine! (machine-name))
          ["help"] (put-str! help)
          ["-h"] (put-str! help)
          ["--help"] (put-str! help)
          (/cons 'cmd _) (cmd-parse-failure (string-append "Unknown command: " cmd))
          [] (put-str! help))))

