#!/usr/bin/env radicle

(load! (find-module-file! "prelude.rad"))

(import prelude/key-management :as 'key-mgmt)

(def help
  "rad create-keys - Radicle Key Management CLI

   Usage:
        rad create-keys [--force]
        rad create-keys help

   Create a new key pair with `rad create-keys`. If `--force` is appended, an
   existing key pair file will be overwritten.
   Per default, key pairs are stored in `$HOME/.config/radicle/my-keys.rad`
   this can be adjusted by setting `$XDG_CONFIG_HOME`.

   The key pair is used for signing actions in the apps. Note that some actions
   are restricted to certain key pairs so deleting or overwriting your key pair
   can lead to loss of some authorisations.")

(def make-keys
  (fn []
    (def key-path (key-mgmt/create-keys!))
    (put-str! (string-append "Successfully created " key-path))
    ))

(def safe-make-keys
  (fn []
    (match (key-mgmt/read-keys!)
      :nothing  (make-keys)
      _         (put-str! "The `my-keys.rad` file already exists.
If you are sure that you want to replace your key pair, run `rad-create-keys --force`"))))

(def args (get-args!))
(match args
  ["--force"] (make-keys)
  []          (safe-make-keys)
  _           (put-str! help))
