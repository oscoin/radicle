#!/usr/bin/env radicle

(load! (find-module-file! "prelude.rad"))
(load! (find-module-file! "monadic/issues.rad"))
(load! (find-module-file! "monadic/project.rad"))
(load! (find-module-file! "prelude/io-utils.rad"))

(import prelude/validation :as 'validation)
(import prelude/machine :as 'machine)

(def machine-name (fn [] (get-rsm-of-type! :rad-issues)))

(def valid-states ["open" "closed"])

(def status-opts ["--filter-by-state" "--state" "-s"])

(def list-opts-help
  (string-append
    "[("
    (string/intercalate " | " status-opts)
    ") <state>] [--fancy]"))

(def help
  (string-append
  "rad-issues - Radicle issue CLI

   Usage:
        rad-issues list " list-opts-help "
        rad-issues new
        rad-issues close <issue-number>
        rad-issues comment <issue-number> <comment>
        rad-issues help

     list         - Select from issues to view or comment on
     new          - Create a new issue in $EDITOR
     close        - Close an issue
     comment      - Shortcut for adding a comment
     help         - Print this help and exit
   "))

(def cmd-parse-failure
  (fn [error]
    (parse-failure error help)))

(def comment-divider "---")

(def verify-issue-number
  (fn [issue-no machine]
    (match (lookup-default issue-no #f (list-issues machine))
      #f          (do
                    (put-str! (string-append "ISSUE " (show issue-no) " does not exist."))
                    (exit! 1))
      'the-issue  the-issue)))


(def add-comment-md
  (fn [x]
    (string-append
     "\n" comment-divider "\n"
     "---Add comment above the line.\n\n"
     x)))

(def pretty-comment
  "Markdown view of a comment. Assumes that the comment has been annotated with a
  username."
  (fn [c]
    (match c
      '{:username u-n
        :created-at c-a
        :body b}
      (string-append "### " u-n " [" c-a "]\n\n" b "\n"))))

(def pretty-issue
  "Markdown view of an issue. Assumes the issue and comments have been annotated
  with usernames."
  (fn [i]
    (match i
      '{:number n
        :title tit
        :body b
        :state state
        :labels ls
        :comments cs
        :username un
        :created-at c-a}
      (string-replace
       "\r"
       ""
       (string-append
        "# Issue " (show n) ": " tit "\n\n"
        "created by " un " [" c-a "]\n\n"
        "**State:** " (if (eq? state :open) "open" "closed") "\n"
        "**Labels:** " (show ls) "\n\n"
        b "\n\n"
        "Comments\n"
        "--------\n\n"
        (string/unlines (map pretty-comment cs)))))))

(def list-items
  "Lists the recent items and uses an editor to add a relevant comment."
  (fn [machine options]

    (def add-username
      (fn [d]
        (def n (lookup-default :github-username (lookup-default :git-username "???" d) d))
        (insert :username n d)))

    (def add-username-issue
      (fn [i]
        (add-username
         (insert :comments (map add-username (lookup :comments i)) i))))

    (def mk-key
      (fn [v]
        (match v
          (/as 'i {:number 'n :title 'tit})
            [(string-append "Issue " (show n) " - " tit) i])))

    (def filt
      (match (map (fn [opt] (read (string-append ":" opt))) (lookup :state options))
        []      (fn [x] #t)
        'args   (fn [x] (elem? (lookup :state x) args))))

    (def items
      (map mk-key
        (filter filt
          (reverse (values (list-issues machine))))))

    (def items-dict (dict-from-seq items))
    (if (empty-seq? items)
      (put-str! "No issues!")
      (if (lookup :fancy options)
        (match (fzf-select-with-preview!
                 (map first items)
                 (fn [s] (pretty-issue (add-username-issue (lookup s items-dict)))))
               (/just 's)
               (do (def selected (lookup s items-dict))
                   (def issue-number (match selected {:number 'n} n))
                   (def pretty-item
                     (add-comment-md
                      (pretty-issue
                       (add-username-issue selected))))
                   (def edited-item (edit-in-editor! pretty-item))
                   (def added-comment
                     (string/unlines
                      (take-while (fn [x] (not (eq? x comment-divider)))
                                  (string/lines edited-item))))
                   (put-str!
                    (if (eq? added-comment "")
                      "Item was not updated."
                      (do (simple-add-comment! machine issue-number added-comment (get-git-username!))
                          (string-append "Sent comment: " added-comment)))))
               _ (put-str! "No selection made."))
        (map (fn [d] (put-str! (first d))) items)))))

(def close-issue
  (fn [issue-number machine]
    (def n (read issue-number))
    (if (number? n)
      (if (integral? n)
        (do
          (verify-issue-number n machine)
          (simple-edit-issue! machine n {:state :closed}))
        (cmd-parse-failure "Issue numbers must be whole numbers!"))
      (cmd-parse-failure "To close an issue, please provide the issue number."))))

(def create-comment!
  "Add a new `comment` to ISSUE."
  (fn [machine n comment]
    (if (number? n)
      (if (integral? n)
        (do
          (verify-issue-number n machine)
          (simple-add-comment! machine n comment (get-git-username!))
          (put-str! (string-append "Added comment to issue " (show n))))
        (cmd-parse-failure "Issue numbers must be whole numbers!"))
    (cmd-parse-failure "To create a comment, please provide the issue number."))))

(def new-issue
  (fn [machine]
    (def template
";; Issue template. An empty or invalid value will abort.
{:title      \"Pick a title\"
 :body       \"Pick a body\"
 :labels     []
}
")
    (def i (read (edit-in-editor! template)))
    (def author {:git-username (get-git-username!)})
    (match (create-issue!  machine (simple-issue (<> i author)))
           ['n] (put-str! (string-append "Created issue #" (show n) " in " machine))
           _    (put-str! "Issue created but unexpected results returned."))))

(def cmd-options
  [
    { :key :state :type :multi-opt :options ["-s" "--state" "--filter-by-state"] :possible-values valid-states :default []}
    { :key :fancy :type :flag :options ["--fancy"] :default #f }
  ])

(def /list-cmd
  (fn [opts]
    (/cmd-opts "list" opts cmd-options help)))

(def args (get-args!))

(machine/catch-daemon!
 (fn []
   (match args
          (/list-cmd 'options) (list-items (machine-name) options)
          (/cmd-0 "new" help) (new-issue (machine-name))
          (/cmd-1 "close" 'issue-number help) (close-issue issue-number (machine-name))
          (/cmd-2 "comment" 'issue-number 'comment help) (create-comment! (machine-name) (read issue-number) comment)
          ["help"] (put-str! help)
          ["-h"] (put-str! help)
          ["--help"] (put-str! help)
          (/cons 'cmd _) (cmd-parse-failure (string-append "Unknown command: " cmd))
          [] (put-str! help))))
